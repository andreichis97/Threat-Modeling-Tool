PROCEDURE global GET_MODEL_NAMESPACES modelId: integer result: reference
{
    SETL prefixesValues:("")
    CC "Core" GET_CLASS_ID objid:(modelId)
    CC "Core" GET_CLASS_NAME classid:(classid)
    CC "Core" GET_ATTR_ID classid:(classid) attrname:"Model Namespaces"
    SETL modelNamespacesId:(attrid)
    #attrid
    CC "Core" GET_ALL_REC_ATTR_ROW_IDS objid:(modelId) attrid:(modelNamespacesId)
	FOR i in:(rowids)
	{
		CC "Core" GET_ATTR_VAL objid:(VAL i) attrname:"Prefix"
		SETL prefixValue:(val)
		CC "Core" GET_ATTR_VAL objid:(VAL i) attrname:"Dictionary"
		SETL dictionaryValue:(val)
        SETL prefixesValues:(prefixesValues + "@prefix " + prefixValue + " " + dictionaryValue + ".\n")
	}
    SETL prefixesValues:(prefixesValues + "\n")
    SETL result:(prefixesValues)
}

PROCEDURE global GET_THREAT_ATTRIBUTES objId: integer uri: string result: reference
{
    CC "Core" GET_CLASS_ID objid:(objId)
    SETL threatAttrs:("")
    SETL threatAttrs:(threatAttrs + uri + " a " + "ex:Threat" + ".\n")
    CC "Core" GET_ATTR_VAL objid:(objId) attrname:"Description"
    SETL threatDescription:(val)
    IF(threatDescription != "")
    {
        SETL threatAttrs:(threatAttrs + uri + " ex:hasDescription " + "\"" + threatDescription + "\"" + "^^" + "xsd:string" + ".\n")
    }
    SETL result:(threatAttrs)
}

PROCEDURE global GET_MITIGATION_ATTRIBUTES objId: integer uri: string result: reference
{
    CC "Core" GET_CLASS_ID objid:(objId)
    SETL mitigationAttrs:("")
    SETL mitigationAttrs:(threatAttrs + uri + " a " + "ex:Mitigation" + ".\n")
    CC "Core" GET_ATTR_VAL objid:(objId) attrname:"Description"
    SETL mitigationDescription:(val)
    IF(mitigationDescription != "")
    {
        SETL mitigationAttrs:(mitigationAttrs + uri + " ex:hasDescription " + "\"" + mitigationDescription + "\"" + "^^" + "xsd:string" + ".\n")
    }
    SETL result:(mitigationAttrs)
}


#-------------------------
CC "Modeling" GET_ACT_MODEL
SETL actModelId: (modelid)

CC "Core" GET_ATTR_VAL objid:(actModelId) attrname:"Model Prefix"
SETL modelPrefix:(val)

CC "Core" GET_ATTR_VAL objid:(actModelId) attrname:"Model Local identifier"
SETL modelLocalIdentifier:(val)

SETL namespaces:("")
SETL endMessage:("")

GET_MODEL_NAMESPACES modelId:(actModelId) result:namespaces

IF(modelPrefix != "" AND modelLocalIdentifier != "")
{
    SETL message:(modelPrefix + modelLocalIdentifier + "\n" + "{" + "\n")
    SETL message:(namespaces + message)
    SETL endMessage:("}")

}
ELSE
{
    SETL message:("")
    SETL message:(message + namespaces)
}

CC "Core" GET_ALL_OBJS_OF_CLASSNAME modelid:(actModelId) classname:"Threat"
SETL threatObjids:(objids)
FOR i in:(threatObjids)
{
    CC "Core" GET_ATTR_VAL objid:(VAL i) attrname:"Object URI"
    SETL threatUri:(val)
    CC "Core" GET_ATTR_VAL objid:(VAL i) attrname:"Name"
    SETL label:(val)
    SETL message:(message + threatUri + " rdfs:label " + "\"" + label + "\"" + ".\n")
    GET_THREAT_ATTRIBUTES objId:(VAL i) uri:(threatUri) result:threatAttrs 
    SETL message:(message + threatAttrs)
    SETL threatAttrs:("")
}

CC "Core" GET_ALL_OBJS_OF_CLASSNAME modelid:(actModelId) classname:"Mitigation"
SETL mitigationObjids:(objids)
FOR i in:(mitigationObjids)
{
    CC "Core" GET_ATTR_VAL objid:(VAL i) attrname:"Object URI"
    SETL mitigationUri:(val)
    CC "Core" GET_ATTR_VAL objid:(VAL i) attrname:"Name"
    SETL label:(val)
    SETL message:(message + mitigationUri + " rdfs:label " + "\"" + label + "\"" + ".\n")
    GET_MITIGATION_ATTRIBUTES objId:(VAL i) uri:(mitigationUri) result:mitigationAttrs 
    SETL message:(message + mitigationAttrs)
    SETL mitigationAttrs:("")
}

CC "Core" GET_ALL_CONNECTORS modelid:(actModelId)
FOR i in:(objids)
{
    CC "Core" GET_CLASS_ID objid:(VAL i)
    CC "Core" GET_CLASS_NAME classid:(classid)
    CC "Core" GET_CONNECTOR_ENDPOINTS objid:(VAL i)
    CC "Core" GET_ATTR_VAL objid:(fromobjid) attrname:"Object URI"
    SETL fromUri:(val)
    CC "Core" GET_ATTR_VAL objid:(toobjid) attrname:"Object URI"
    SETL toUri:(val)
    SETL message:(message + fromUri + " ex:mitigatedBy " + toUri + ".\n")
}

IF (endMessage != "")
{
    SETL message:(message + endMessage)
    CC "AdoScript" VIEWBOX text:(message)
    SETL filePath:("D:\\Andrei\\ModellingTools\\Threat_Modeling_Tool\\Scripts\\Results\\threat_diagram_graph.trig")
    CC "AdoScript" FWRITE file:(filePath) text:(message) binary:0
    SETL filePath:("D:\\Andrei\\ModellingTools\\Threat_Modeling_Tool\\Scripts\\Results\\threat_serialization_type.txt")
    CC "AdoScript" FWRITE file:(filePath) text:("trig") binary:0
}
ELSE
{
    CC "AdoScript" VIEWBOX text:(message)
    SETL filePath:("D:\\Andrei\\ModellingTools\\Threat_Modeling_Tool\\Scripts\\Results\\threat_diagram_graph.ttl")
    CC "AdoScript" FWRITE file:(filePath) text:(message) binary:0
    SETL filePath:("D:\\Andrei\\ModellingTools\\Threat_Modeling_Tool\\Scripts\\Results\\threat_serialization_type.txt")
    CC "AdoScript" FWRITE file:(filePath) text:("turtle") binary:0
}